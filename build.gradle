plugins {
  id 'java-library'
  id 'eclipse'
  id 'org.checkerframework' version '0.5.17' apply false
}

description """Simple datastructures and algorithms."""

allprojects {
  group = 'com.github.basking2'
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  ext.releaseRepository = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
  ext.snapshotRepository = "https://oss.sonatype.org/content/repositories/snapshots/"

  repositories {
    mavenCentral()
    jcenter()
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

}

subprojects { subproject ->
  apply plugin: 'org.checkerframework'

  checkerFramework {
    checkers = ['org.checkerframework.checker.iteration.IterationChecker']
    extraJavacArgs = [
            '-AsuppressWarnings=contracts.conditional.postcondition.not.satisfied'
    ]
  }
  // To use a locally-built Checker Framework, run gradle with "-PcfLocal".
  if (project.hasProperty("cfLocal")) {
    def cfHome = String.valueOf(System.getenv("CHECKERFRAMEWORK"))
    dependencies {
      compileOnly files(cfHome + "/checker/dist/checker-qual.jar")
      testCompileOnly files(cfHome + "/checker/dist/checker-qual.jar")
      checkerFramework files(cfHome + "/checker/dist/checker.jar")
    }
  }

}

